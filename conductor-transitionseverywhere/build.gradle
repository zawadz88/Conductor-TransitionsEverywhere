apply plugin: 'com.android.library'
apply plugin: 'maven'

ext {
    bintrayRepo = 'maven'
    bintrayName = POM_ARTIFACT_ID

    publishedGroupId = POM_GROUP_ID
    libraryName = 'Conductor-TransitionsEverywhere'
    artifact = POM_ARTIFACT_ID

    libraryDescription = 'A Conductor extension which allows to use TransitionsEverywhere\'s transitions when swapping controllers.'

    siteUrl = 'https://github.com/zawadz88/Conductor-TransitionsEverywhere'
    gitUrl = 'https://github.com/zawadz88/Conductor-TransitionsEverywhere.git'

    libraryVersion = POM_VERSION

    developerId = 'zawadz88'
    developerName = 'Piotr Zawadzki'
    developerEmail = 'zawadz88@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion project.androidCompileSdkVersion
    buildToolsVersion("$androidBuildToolsVersion")


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion project.androidMinSdkVersion
        targetSdkVersion project.androidTargetSdkVersion
    }

}

dependencies {
    compile("com.andkulikov:transitionseverywhere:$transitionsEverywhereVersion")
    compile("com.bluelinelabs:conductor:$conductorVersion")

    //Force the latest Support library version
    compile("com.android.support:support-annotations:$androidSupportLibraryVersion") {
        force true
    }
    compile("com.android.support:support-v4:$androidSupportLibraryVersion") {
        force true
    }
}

gradle.taskGraph.beforeTask { Task task ->
    if (task instanceof Javadoc) {
        def releaseVariant = android.libraryVariants.find { it.buildType.name == 'release' }
        Javadoc javadocTask = (Javadoc) task
        javadocTask.source = android.sourceSets.main.java.srcDirs
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        javadocTask.classpath = files(releaseVariant.javaCompile.classpath.files) + files(ext.androidJar)
    }
}

apply from: '../installv1.gradle'
apply from: '../bintrayv1.gradle'
